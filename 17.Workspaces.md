# 17. Workspaces <!-- omit in TOC -->

## 1. Crear la carpeta /workspaces

## 2. Crear dos Workspaces
> Por defecto, Terraform crea un workspace inicial.

```vim
terraform workspace new segundo
terraform workspace new tercero
```
> Created and switched to workspace "segundo"!
>
> Created and switched to workspace "tercero"!

> Se crea el directorio terraform.tfstate.d

## 3. Seleccionar el workspace segundo
```tf
terraform workspace select segundo
```
> Switched to workspace "segundo".

## 4. Listar
```vim
terraform workspace list
```
```vim
  default
* segundo
  tercero
```

## 5. Crear terraform.tf
```tf
terraform {
  required_providers {
    aws = {
      version = "~> 4.0"
    }
  }
}
```

## 6. Crear variables.tf
```tf
variable "region" {
  type = map(any)
  default = {
    "segundo" = "us-east-1"
    "tercero" = "us-east-2"
  }

}
variable "ami" {
  type = map(any)
  default = {
    "segundo" = "ami-033b95fb8079dc481"
    "tercero" = "ami-0b614a5d911900a9b"
  }
}
```
> Map = Objeto
>
> [Mas info Map](https://www.terraform.io/language/expressions/types#map)

## 7. Crear provider.tf
```tf
provider "aws" {
  region = lookup(var.region, terraform.workspace)
}
```
> Lookup retorna un valor usando su "key"
>
> Utiliza terraform.workspace como variable, en este caso = segundo
>
> Ej: key = segundo -> "us-east-1"

> [Mas info Lookup](https://www.terraform.io/language/functions/lookup)

## 8. Crear la carpeta ./modules, archivos:

### 8.1. variables.tf
```tf
variable "server_region" {
  type    = string
}
variable "ami" {
  type    = string
}
```
### 8.2. server.tf
```tf
resource "aws_default_vpc" "default" {
  tags = {
    Name = "Default VPC"
  }
}

resource "aws_instance" "server" {
  ami           = var.ami
  instance_type = "t2.nano"
  tags = {
    Name = "${var.server_region}-server"
  }
}
```

### output.tf
```tf
output "instance_size" {
  value = aws_instance.server.instance_type
}

output "instance_id" {
  value = aws_instance.server.id
}

output "availability_zone" {
  value = aws_instance.server.availability_zone
}

output "instance_name" {
  value = aws_instance.server.tags.Name
}
```

## 9. Crear en el directorio /workspaces, el archivo main.tf
```tf
module "server" {
  source        = "./modules"
  server_region = lookup(var.region, terraform.workspace)
  ami           = lookup(var.ami, terraform.workspace)
}
```
> Utiliza lookup para definir la variable segÃºn el workspace seleccionado

## Crear output.tf
```tf
output "instance_size" {
  value = module.server.instance_size
}

output "instance_id" {
  value = module.server.instance_id
}

output "availability_zone" {
  value = module.server.availability_zone
}

output "instance_name" {
  value = module.server.instance_name
}
```

## 10. Iniciar y aplicar
```vim
Outputs:

availability_zone = "us-east-1b"
instance_id = "i-0e9dd4c9dde64cbfc"
instance_name = "us-east-1-server"
instance_size = "t2.nano"
```

## Cambiar al workspace tercero y aplicar
```vim
terraform workspace select tercero
```
> Switched to workspace "tercero".

```vim
terraform plan
terraform apply
```

```vim
Outputs:

availability_zone = "us-east-2c"
instance_id = "i-00e947dfc4843b81e"
instance_name = "us-east-2-server"
instance_size = "t2.nano"
```
> validar la zona 2

## Eliminar los recursos de ambos workspaces
```vim
terraform destroy -auto-approve
terraform workspace select segundo
terraform destroy -auto-approve
```
